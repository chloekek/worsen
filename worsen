#!/usr/bin/env perl

use strict;
use warnings;

if (@ARGV == 0) {
    print STDERR "Usage: $0 FILE [FILE ...]\n";
    exit 1;
}

my $MACRO_NAME = qr/[A-Za-z0-9_-]+/;

# Mapping from macro names to arrays of lines.
my %macros;

for (@ARGV) {
    open my $file, '<', $_
        or die "Cannot open $_: $!";

    # The name of the current macro, or undef if there is no current macro.
    my $macro;

    # The source lines of the current macro.
    my @source;

    # Loop through the file, keeping track of the current macro, and committing
    # completed macros into the %macros hash.
    while (<$file>) {

        # A macro begin marker begins a new macro.
        if (/^MACRO ($MACRO_NAME)$/) {
            die "Already in a macro definition of $macro" if defined $macro;
            die "Redefinition of macro $1" if exists $macros{$1};
            $macro = $1;
            next;
        }

        # A macro end marker ends the current macro.
        if (/^END MACRO$/) {
            die "Not in a macro definition" unless defined $macro;
            $macros{$macro} = [@source];
            $macro = undef;
            @source = ();
            next;
        }

        # If we are currently define a macro, commit the line to the @source
        # array so it can be included in the macro.
        if (defined $macro) {
            push @source, $_;
            next;
        }
    }
}

sub include_macro
{
    my $macro = shift;
    my $result = '';
    die "Including undefined macro $macro"
        unless exists $macros{$macro};
    $result .= s/INCLUDE MACRO ($MACRO_NAME)/include_macro($1)/egr
        for $macros{$macro}->@*;
    $result;
}

for my $macro (keys %macros) {
    print "--------\n";
    print "$macro\n";
    print include_macro($macro);
}
